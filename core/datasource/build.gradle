plugins{
    id 'java'
    alias(libs.plugins.sonar)
    id 'jacoco'
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility=17
targetCompatibility=17

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['src-resource']
    test.java.srcDirs = ['tst']
    test.resources.srcDirs = ['tst-resource']
}

configurations{
    libJar
    configurations.implementation.extendsFrom(configurations.libJar)
}

configurations.all{
    exclude group: 'jakarta.inject', module: 'jakarta.inject-api'
    exclude group: 'org.glassfish.jaxb', module: 'jaxb-runtime'
    exclude group: 'io.smallrye', module: 'jandex'
}

dependencies {
    libJar libs.bundles.orm
    compileOnly libs.bundles.log

    testImplementation libs.bundles.junit
}

jar {
    manifest {
        attributes (
			'Multi-Release': 'true'
		)
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.libJar.collect { it.isDirectory() ? it : zipTree(it) } }
}

test {
    testLogging.events = ["passed", "skipped", "failed"]
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports{
        xml.required = true
    }
}

tasks['sonar'].dependsOn jacocoTestReport